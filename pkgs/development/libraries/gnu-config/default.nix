{ stdenv, fetchurl }:

let
  rev = "bb8fb4029563dcd564ece143ce558ea44c720a15";

  # Don't use fetchgit as this is needed during Aarch64 bootstrapping
  configGuess = fetchurl {
    url = "http://git.savannah.gnu.org/cgit/config.git/plain/config.guess?id=${rev}";
    sha256 = "0sbcyn617s0cvrmj4phjfwh96yx9vni77fyxnhi9wqiqnw8x1fcr";
  };
  configSub = fetchurl {
    url = "http://git.savannah.gnu.org/cgit/config.git/plain/config.sub?id=${rev}";
    sha256 = "1lm1wg0nlk19b6dj3afbcr83dqb9xffshm611zm89d57z0fkdwsz";
  };
in
stdenv.mkDerivation rec {
  name = "gnu-config-${version}";
  version = "2016-02-11";

  buildCommand = ''
    mkdir -p $out
    cp ${configGuess} $out/config.guess
    cp ${configSub} $out/config.sub
  '';

  meta = with stdenv.lib; {
    description = "Attempt to guess a canonical system name";
    homepage = http://savannah.gnu.org/projects/config;
    license = licenses.gpl3;
    # In addition to GPLv3:
    #   As a special exception to the GNU General Public License, if you
    #   distribute this file as part of a program that contains a
    #   configuration script generated by Autoconf, you may include it under
    #   the same distribution terms that you use for the rest of that
    #   program.
    maintainers = [ maintainers.dezgeg ];
    platforms = platforms.all;
  };
}
